# Unidad 7. Xquery

## Ejercicio 1

1. **Lista el título de los libros ordenados por título.**
   ```xq
   (: Consulta1 Lista libros:)
   for $x in doc('books')/bookstore/book
   order by $x/title
   return $x/title/text()
   ```         
   > Resultado [Listado de libros](resultado01.txt)
2. **Lista el título de los libros ordenados por título cuyo precio es superior a 30.**
   ```xq
   (: Consulta2 Lista libros precio superior a 30:)
    for $x in doc('books')/bookstore/book
    order by $x/title
    where $x/price>30
    return $x/title/text()
   ```
   > Resultado [Listado de libros > 30](resultado02.txt)
3. **Crea una página web (03.html) cuyo título de la web “UD7-01.03 Listado de libros. Nombre y
   apellidos” y que contenga una lista con viñetas con los títulos de los libros.**
   ```html
   (: Consulta3:)
    <html>
        <head>
            <title>Lista de alumnos</title>
        </head>
        <body>
            <ul>
                {
                    for $x in doc('books')/bookstore/book
                    order by $x/title
                    where $x/price>30
                    return <li>{$x/title/text()}</li>
                }
             </ul>
        </body>
    </html>
    ```
    > Resultado [Página HTML](resultado03.html)
4. **Crea un documento XML(04.html), donde el elemento raíz es libros. Y si la categoría del libro es
   children el título aparece entre las etiquetas <children> y en caso contrario aparece con la etiqueta
   <adult>.**
   ```xml
    (: Consulta4:)
    <books>
        {
            for $x in doc('books')//book
            return if ($x/@category="children")
            then <child>{$x/title/text()}</child>
            else <adult>{$x/title/text()}</adult>
        }
    </books>
    ``` 
    > Resultado [Documento XML](resultado04.xml)
5. **Crea una página web (05.html), que contenga todos los libros con su categoría, en la lista aparece
   distintos colores el título en función de la categoría.**
   ```html
   (: Consulta 5 :)
    <html>
        <head>
            <title>Ud7-1 VÍCTOR GARCÍA GORDÓN</title>
            <link href="css/estilos.css" type="text/css" rel="stylesheet"/>
        </head>
        <body>
                <header>
                    <h1>Bookstore</h1>
                </header>
                <main>
                    <article>
                        <table>
                            <thead>
                                <tr>
                                    <th>Título</th>
                                    <th>Categoría</th>
                                </tr>
                            </thead>
                            <tbody>
                            {
                                for $x in doc('books')/bookstore/book
                                return <tr>
                                        <td>{data($x/title)}</td>
                                        <td>{data($x/@category)}</td>
                                    </tr>
                            }
                            </tbody>
                        </table>
                    </article>
                    <article>
                        <ul>
                        {
                            for $x in doc('books')/bookstore/book
                            return
                            <li class="{data($x/@category)}">{data($x/title)}</li>
                        }
                        </ul>
                    </article>
                </main>    
        </body>
    </html>
    ``` 
    > Resultado [Página HTML](resultado05.html)
6. **Lista de libros ordenados por categoría de forma descendiente, y como segundo criterio por título
   de forma descendiente. Mostrando el título con suprecio precio, donde el precio es mayor que 30 $.
   El resultado (06.txt) es un fichero de texto**
    ```xq
    (: Consulta 6 :)
    let $x := doc("books")
    for $y in $x/bookstore/book
    where $y/price > 30
    order by $y/@category descending, $y/title descending
    return concat($y/title, ":", $y/price, "$")
    ``` 
    > Resultado [Fichero txt](resultado06.txt)
7. **Uso de at, for variable1 at variable2 expresiónXpath, siendo la segunda variable la posición de
   cada nodo. Genera el siguiente archivo XML (07.xml)**
    ```xml
    (: Consulta 7 :)
    <books>
    {
    for $x at $y in doc('books')/bookstore/book
    return  <book>{$y}. {data($x/title)}</book>
    }
    </books>
    ``` 
    > Resultado [Documento XML](resultado07.xml)
8. **Uso de variables expresiones en un for $tema in (lista de valores), $y in (lista de valores). Para
   indicar la lista de valor de valorinicial to valorfinal, por ejemplo 1 to 100
   Generar automáticamente la siguiente página web (08.html)**
    ```html
    (: Consulta 8 :)
    <html>
    <head>
    <meta charset="UTF-8"/>
    <title>Prácticas Víctor García Gordón</title>
    </head>
    <body>
    <h1>Listado de practicas</h1>
    {
    for $tema in(1 to 7),$y in(1 to 3)
    return 
    <div>
        <a href="{$tema}/{$y}.html">Tema{$tema} Ejercicio{$y}</a>
    </div>  
    }
    </body>
    </html>
    ```
    > Resultado [Página HTML](resultado08.html)
9. **Mostrar el título del libro cuando tenga más de dos autores.**
    ```xq
    (: Consulta 9 :)
    for $x in doc("books")/bookstore/book
    let $a := $x/author
    where count($a)>2
    return $x/title/text()
    ```
   > Resultado [Fichero txt](resultado09.txt)
10. **Trasformar los datos a un archivo XML con el siguiente contenido, donde el atributo autores indica
    el número de autores de cada libro**
    ```xml
    (: Consulta 10 :)
    <libros>
    {
    for $x in doc("books")/bookstore/book
    let $y := count($x/author)
    return <libro autores="{$y}">{$x/title/text()}</libro>
    }
    </libros>
    ```
    > Resultado [Documento XML](resultado10.xml)
11. **Mostrar el título y el autor de los libros del año 2005, y etiquetar cada uno de ellos con “lib2005”.**
    ```xml
    (: Consulta 11 :)
    <libros>{
    for $x in doc('books')//book[year = 2005]
    return(
        <lib2005>
            <titulo>{$x/title}</titulo>
            <autor>{$x/author}</autor>
        </lib2005>
    )   
    }
    </libros>
    ```
    > Resultado [Documento XML](resultado11.xml)
12. **Mostrar los años de publicación, primero con for y luego con let. Etiquetar la salida con publicación**
    ```xml
    (: Consulta 12 :)
    (:
    <libros>
    {
    for $x in distinct-values(doc('books')//book/year)
    return <publicacion>{$x}</publicacion>
    }
    </libros> 
    :)

    (: Con let :)
    <libros>
    {
    let $x := distinct-values(doc('books')//book/year)
    for $x in $x
    return <publicacion>{$x}</publicacion>
    }
    </libros>
    ```
    > Resultado [Documento XML](resultado12.xml)
13. **Mostrar los libros ordenador primero por “category” y luego por “title” en una sola consulta.**
    ```xml
    (: Consulta 13 :)
    <libros>
    {
    for $x in doc('books')//book
    order by $x/@category, $x/title
    return $x  
    }
    </libros>
    ```
    > Resultado [Documento XML](resultado13.xml)
14. **Mostrar cuántos libros hay, y etiquetarlo con “total”**
    ```xml
    (: Consulta 14 :)
    let $x := count(doc("books")//book)
    return <total>{$x}</total>
    ```
    > Resultado [Documento XML](resultado14.xml)
15. **Mostrar los títulos de los libros y al final una etiqueta con el número total de libros**
    ```xml
    (: Consulta 15 :)
    <libros>{
    for $x in doc('books')/bookstore
    return ($x/book/title, <total>{count($x/book)}</total>)
    }
    </libros>
    ```
    > Resultado [Documento XML](resultado15.xml)
16. **Mostrar el precio mínimo y el máximo de los libros**
    ```xml
    (: Consulta 16 :)
    <libros>
    {
    let $x := doc('books')//book/price
    return (
        <min>{min($x)}</min>,
        <max>{max($x)}</max>
    ) 
    }
    </libros>
    ```
    > Resultado [Documento XML](resultado16.xml)
17. **Mostrar el título del libro, su precio y su precio con el IVA incluido, cada uno con su propia etiqueta.
    Ordenado de precio con IVA**
    ```xml
    (: Consulta 17 :)
    <libros>
    {
    let $x := doc('books')//book/price
    return (
        <min>{min($x)}</min>,
        <max>{max($x)}</max>
    )  
    }
    </libros>
    ``` 
    > Resultado [Documento XML](resultado17.xml)
18. **Mostrar la suma total de los precios con la etiqueta total**
    ```xml
    (: Consulta 18 :)
    let $x := sum(doc('books')//book/price)
    return <total>{$x}</total>
    ```
    > Resultado [Documento XML](resultado18.xml)
19. **Mostrar cada uno de los precios de los libros , y al final una etiqueta con la suma de los precios.**
    ```xml
    (: Consulta 19 :)
    <libros>
    {
    let $x := doc('books')//book/price
    return (
        for $y in $x
        return $y ,
        <total>{sum($x)}</total>
    ) 
    }
    </libros>
    ```
    > Resultado [Documento XML](resultado19.xml)
20. **Mostrar el título y el número de autores que tiene cada título en etiquetas diferentes.**
    ```xml
    (: Consulta 20 :)
    <libros>
    {
    for $x in doc('books')//book
    return (
            <titulo>{$x/title}</titulo>,
            <numautores>{count($x/author)}</numautores>  
    )        
    }
    </libros>
    ```
    > Resultado [Documento XML](resultado20.xml)
21. **Mostrar en la misma etiqueta el título y entre paréntesis el número de autores que tiene ese título**
    ```xml
    (: Consulta 21 :)
    <libros>
    {
    for $x in doc('books')//book
    return (
        concat($x/title, " (", count($x/author), ")")
    )    
    }      
    </libros>
    ```
    > Resultado [Documento XML](resultado21.xml)
22. **Mostrar los libros escritos en años que terminan en “3”**
    ```xml
    (: Consulta 22 :)
    <libros>
    {
    for $x in doc('books')//book[substring(year, string-length(year)) = "3"]
    return $x 
    }
    </libros>

    (:
    <libros>
    {
    for $x in doc('02')/book
    where ends-with($x/year,'3')
    return data($x/title)  
    }
    </libros>
    :)
    ```
    > Resultado [Documento XML](resultado22.xml)
23. **Mostrar los libros cuya categoría empiece por C**
    ```xml
    (: Consulta 23 :)
    <libros>
    {
    for $x in doc('books')//book[starts-with(lower-case(@category), 'c')]/title
    return $x 
    }
    </libros>
    ```
    > Resultado [Documento XML](resultado23.xml)
24. **Mostrar los libros que contenga una X mayúscula o minúscula en el título ordenados de manera
    descendente**
    ```xml
    (: Consulta 24 :)
    <libros>
    {
    for $x in doc('books')//book[contains(translate(title, 'x', 'X'), 'X')]
    order by $x/title descending
    return $x 
    }
    </libros>
    ``` 
    > Resultado [Documento XML](resultado24.xml)
25. **Mostrar el título y el número de caracteres que tiene cada título, cada uno con su propia etiqueta.**
    ```xml
    (: Consulta 25 :)
    <libros>
        {
            for $x in doc('books')//book
            return (
                <libro>
                    <titulo>{data($x/title)}</titulo>
                    <caracteres>{string-length($x/title)}</caracteres>
                </libro>
            )
        }
    </libros>
    ```
    > Resultado [Documento XML](resultado25.xml)
26. **Mostrar todos los años en los que se ha publicado un libro eliminando los repetidos. Etiquétalos con
    "año".**
    ```xml
    (: Consulta 26 :)
    <anyos>
        {
            for $x in distinct-values(doc('books')//book/year)
            return <anyo>{$x}</anyo>
        }
    </anyos>
    ``` 
    > Resultado [Documento XML](resultado26.xml)
27. **Mostrar todos los autores eliminando los que se repiten y ordenados por el número de caracteres
    que tiene cada autor.**
    ```xml
    (: Consulta 27 :)
    <autores>
        {
            for $x in distinct-values(doc('books')//book/author)
            order by string-length($x)
            return <autor>{$x}</autor>
        }
    </autores>
    ``` 
    > Resultado [Documento XML](resultado27.xml)
28. **Mostrar los títulos en una tabla de HTML.**
    ```html
    (: Consulta 28 :)
    let $x := doc('books')//book
    return
    <html>
        <body>
            <table border="1">
                <tr>
                    <th>Título</th>
                </tr>
                {
                    for $y in $x
                    return
                        <tr>
                            <td>{$y/title}</td>
                        </tr>
                }
            </table>
        </body>
    </html>
    ```
    > Resultado [Página HTML](resultado28.html)
